# Shader
file(GLOB_RECURSE ALBEDO_VERTEX_SHADER_SOURCE_FILES
    "${CMAKE_CURRENT_SOURCE_DIR}/shader/source/*.vert"
)
file(GLOB_RECURSE ALBEDO_FRAGMENT_SHADER_SOURCE_FILES
    "${CMAKE_CURRENT_SOURCE_DIR}/shader/source/*.frag"
)
list(APPEND ALBEDO_SHADER_SOURCE_FILES ${ALBEDO_VERTEX_SHADER_SOURCE_FILES})
list(APPEND ALBEDO_SHADER_SOURCE_FILES ${ALBEDO_FRAGMENT_SHADER_SOURCE_FILES})

set(ALBEDO_SHADER_COMPILER "${CMAKE_CURRENT_SOURCE_DIR}/shader/compiler/glslc.exe")

# Chain of Dependence: target(AlbedoShader) -> file(ALBEDO_SHADER_SPIRV_FILES) -> file(ALBEDO_SHADER_SOURCE_FILES)
foreach(SHADER_SOURCE_FILE ${ALBEDO_SHADER_SOURCE_FILES})   
    get_filename_component(FILE_NAME ${SHADER_SOURCE_FILE} NAME)
    set(SHADER_SPIRV_FILE "${CMAKE_CURRENT_SOURCE_DIR}/shader/${FILE_NAME}.spv")

    add_custom_command(
        OUTPUT  ${SHADER_SPIRV_FILE}
        COMMAND ${ALBEDO_SHADER_COMPILER}
        ARGS    ${SHADER_SOURCE_FILE} -o ${SHADER_SPIRV_FILE}
        DEPENDS ${SHADER_SOURCE_FILE}
        COMMENT "Generated ${SHADER_SPIRV_FILE}"
    )
    list(APPEND ALBEDO_SHADER_SPIRV_FILES ${SHADER_SPIRV_FILE})

endforeach(SHADER_SOURCE_FILE)

add_custom_target(
    AlbedoShader
    ALL
    DEPENDS ${ALBEDO_SHADER_SPIRV_FILES}
)

source_group("Vertex Shaders" FILES ${ALBEDO_VERTEX_SHADER_SOURCE_FILES})
source_group("Fragment Shaders" FILES ${ALBEDO_FRAGMENT_SHADER_SOURCE_FILES})

target_sources(AlbedoShader 
    PRIVATE 
    ${ALBEDO_VERTEX_SHADER_SOURCE_FILES}
    ${ALBEDO_FRAGMENT_SHADER_SOURCE_FILES})